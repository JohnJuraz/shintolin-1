#!/usr/bin/env node_modules/coffeescript/bin/coffee

db = require '../../db'
db.connect().then ->

  Bluebird = require 'bluebird'
  async = require 'async'

  get_tile_by_coords = Bluebird.promisify(require '../../queries/get_tile_by_coords')
  calculate_recovery = require '../../queries/calculate_recovery'

  MAX_AP = 100
  MINUTES_BETWEEN_TICK = 10.0

  Bluebird.resolve()

    .then ->
      QUERY =
        ap: {$lt: MAX_AP}
      db.characters().find(QUERY).toArray()

    .each (character) ->
      Bluebird.resolve()
        .then ->
          get_tile_by_coords character
        .then (tile) ->
          calculate_recovery character, tile
        .then (recovery) ->
          (recovery / 60.0) * MINUTES_BETWEEN_TICK # normalize regen to tick cadance
        .then (recovery) ->
          if character.ap + recovery > MAX_AP
            recovery = MAX_AP - character.ap
          return unless recovery > 0

          QUERY =
            _id: character._id
          UPDATE =
            $inc:
              ap: recovery
          db.characters().updateOne QUERY, UPDATE

    .then ->
      process.exit 0
    .catch (err) ->
      console.log err
      process.exit 1
