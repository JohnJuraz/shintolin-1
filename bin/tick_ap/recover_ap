#!/usr/bin/env node_modules/coffee-script/bin/coffee

require 'babel/register'

db = require '../../db'
db.connect().then ->

  Bluebird = require 'bluebird'
  async = require 'async'

  get_tile_by_coords = Bluebird.promisify(require '../../queries/get_tile_by_coords')
  calculate_recovery = require '../../queries/calculate_recovery'

  MAX_AP = 100

  Bluebird.resolve()

    .then ->
      QUERY =
        ap: {$lt: MAX_AP}
      db.characters().find(QUERY).toArray()

    .each (character) ->
      Bluebird.resolve()
        .then ->
          get_tile_by_coords character
        .then (tile) ->
          calculate_recovery character, tile
        .then (recovery) ->
          recovery / 60.0 # recover a bit every minute instead of once an hour
        .then (recovery) ->
          if character.ap + recovery > MAX_AP
            recovery = MAX_AP - character.ap
          return unless recovery > 0

          QUERY =
            _id: character._id
          UPDATE =
            $inc:
              ap: recovery
          db.characters().updateOne QUERY, UPDATE

    .then ->
      process.exit 0
    .catch (err) ->
      console.log err
      process.exit 1
