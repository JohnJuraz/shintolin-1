#!/usr/bin/env node_modules/coffee-script/bin/coffee

db = require '../../db'
db.connect().then ->

  _ = require 'underscore'
  async = require 'async'
  data = require '../../data'
  commands = require '../../commands'
  field = data.buildings.field

  DEFAULT_HP_GROWTH = 3
  WATERED_MULTIPLIER = 3
  OVERGROWTH_DECREASE = .5

  async.series [
    (cb) ->
      query =
        building: 'field'
        hp:
          $gt: field.hp
          $lt: field.hp_max
      db.tiles().find(query).toArray (err, tiles) ->
        return cb(err) if err?
        async.each tiles, (tile, cb) ->
          growth = if tile.watered then (DEFAULT_HP_GROWTH * WATERED_MULTIPLIER) else DEFAULT_HP_GROWTH
          growth = field.hp_max - tile.hp if tile.hp + growth > field.hp_max
          query =
            _id: tile._id
          update =
            $inc:
              hp: growth
          db.tiles().updateOne query, update, cb
        , cb
    (cb) ->
      query =
        overuse:
          $gt: 0
      update =
        $inc:
          overuse: 0 - OVERGROWTH_DECREASE
      db.tiles().updateMany query, update, cb
    (cb) ->
      update =
        $unset:
          watered: true
      db.tiles().updateMany {}, update, cb
  ], (err) ->
    if err?
      console.log err
      process.exit 1
    else
      console.log 'Fields Grown'
      process.exit 0
